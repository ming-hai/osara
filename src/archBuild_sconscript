# OSARA: Open Source Accessibility for the REAPER Application
# SConscript for architecture-specific building of the source
# Author: James Teh <jamie@nvaccess.org>
# Copyright 2015-2016 NV Access Limited
# License: GNU General Public License version 2.0

Import("env")
env.Append(CPPPATH=("#include", "#include/WDL"))

sources = [
	"reaper_osara.cpp",
	"paramsUi.cpp",
	"peakWatcher.cpp",
]

if env["PLATFORM"] == "win32":
	env.Append(CPPFLAGS="/EHsc")
	# We always want debug symbols.
	env.Append(PDB="${TARGET}.pdb")
	# having symbols usually turns this off, but we have no need for unused symbols.
	env.Append(LINKFLAGS='/OPT:REF')
	sources.append(env.RES("reaper_osara.rc"))
	libs = [
		"user32",
		"ole32",
		"oleacc",
		"OleAut32",
	]

else: # Mac
	swellDir = env.Dir("#include/WDL/WDL/swell")
	env.Append(CPPPATH=(swellDir,))
	env["CXX"] = "clang++"
	# Build a universal binary with both 32 and 64 bit.
	archFlags = ("-arch", "i386", "-arch", "x86_64")
	env.Append(CXXFLAGS=archFlags, LINKFLAGS=archFlags)
	env.Append(CXXFLAGS=("-stdlib=libc++", "-std=c++11"))
	# SWELL files.
	sources.extend(swellDir.File(f) for f in (
		"swell.cpp", "swell-ini.cpp", "swell-dlg.mm", "swell-gdi.mm",
		"swell-kb.mm", "swell-misc.mm", "swell-miscdlg.mm", "swell-menu.mm",
		"swell-wnd.mm", "swell-modstub.mm",
	))
	sources.append("osxa11y.mm")
	libs = []
	# OS X frameworks.
	for f in ("Cocoa", "Carbon", "Appkit"):
		env.Append(LINKFLAGS=("-framework", f))
	# We don't want the "lib" prefix for our dylib.
	env["SHLIBPREFIX"] = ""

	# Generate Mac resources.
	res = env.Command(["reaper_osara.rc_mac_dlg", "reaper_osara.rc_mac_menu"], "reaper_osara.rc",
		[["include/WDL/WDL/swell/mac_resgen.php", "$SOURCE"]])
	env.Depends("reaper_osara.cpp", res)

env.SharedLibrary(
	target="reaper_osara%s" % env["libSuffix"],
	source=sources, LIBS=libs,
)
